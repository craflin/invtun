
name = "invtun"

targets = {

  all = {
    dependencies = { "invtunc", "invtuns" }
  }

  invtunc = cppApplication + {
    dependencies = { "libnstd", "liblz4" }
    buildDir = "Build/$(configuration)/Client"
    output = "Build/$(configuration)/$(target)$(if $(Win32),.exe,)"
    includePaths = {
      "Ext/libnstd/include"
      "Ext/lz4"
      "Src"
    }
    libPaths = {
      "Build/$(configuration)/libnstd"
      "Build/$(configuration)/liblz4"
    }
    libs = { "nstd", "lz4" }
    root = { "Src/Client", "Src" }
    files = {
      "Src/Client/**.cpp" = cppSource
      "Src/Client/**.h"
      "Src/Tools/**.cpp" = cppSource
      "Src/Tools/**.h"
      "Src/Protocol.h"
    }
    if tool == "vcxproj" {
      libs += { "ws2_32" }
      linkFlags += { "/SUBSYSTEM:CONSOLE" }
    }
    if platform == "Linux" {
      libs += { "pthread", "rt" }
      cppFlags += { "-Wno-delete-non-virtual-dtor" }
    }
  }
  
  invtuns = cppApplication + {
    dependencies = { "libnstd", "liblz4" }
    buildDir = "Build/$(configuration)/Server"
    output = "Build/$(configuration)/$(target)$(if $(Win32),.exe,)"
    includePaths = {
      "Ext/libnstd/include"
      "Ext/lz4"
      "Src"
    }
    libPaths = {
      "Build/$(configuration)/libnstd"
      "Build/$(configuration)/liblz4"
    }
    libs = { "nstd", "lz4" }
    root = { "Src/Server", "Src" }
    files = {
      "Src/Server/**.cpp" = cppSource
      "Src/Server/**.h"
      "Src/Tools/**.cpp" = cppSource
      "Src/Tools/**.h"
      "Src/Protocol.h"
    }
    if tool == "vcxproj" {
      libs += { "ws2_32" }
      linkFlags += { "/SUBSYSTEM:CONSOLE" }
    }
    if platform == "Linux" {
      libs += { "pthread", "rt" }
      cppFlags += { "-Wno-delete-non-virtual-dtor" }
    }
  }
  
  libnstd = cppStaticLibrary + {
    folder = "Libraries"
    includePaths = { "Ext/libnstd/include" }
    buildDir = "Build/$(configuration)/$(target)"
    root = { "Ext/libnstd/src", "Ext/libnstd/include/nstd" }
    files = {
      "Ext/libnstd/src/*.cpp" = cppSource,
      "Ext/libnstd/include/nstd/*.h"
    }
    if tool == "vcxproj" {
      defines += { "_CRT_SECURE_NO_WARNINGS" }
    }
  }
  
  liblz4 = cppStaticLibrary + {
    folder = "Libraries"
    includePaths = { "Ext/lz4" }
    buildDir = "Build/$(configuration)/$(target)"
    root = { "Ext/lz4" }
    files = {
      "Ext/lz4/lz4.c" = cSource,
      "Ext/lz4/lz4.h"
    }
  }
}
